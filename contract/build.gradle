import groovy.xml.MarkupBuilder
import org.jooq.codegen.GenerationTool

import javax.xml.bind.JAXB

buildscript {
	repositories {
		gradlePluginPortal()
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath "org.glassfish.jaxb:jaxb-runtime:2.3.2"
		classpath "org.postgresql:postgresql:42.2.14"
		classpath "io.github.jklingsporn:vertx-jooq-generate:5.2.0"
	}
}

apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.flywaydb.flyway"
apply plugin: "nu.studer.jooq"

dependencies {
	implementation project(":proto")
	implementation "com.hedera.hashgraph:sdk:2.0.1"
	implementation "io.grpc:grpc-netty-shaded:1.24.0"
	implementation "io.github.cdimascio:java-dotenv:5.2.1"

	implementation "org.apache.logging.log4j:log4j-api:2.14.0"
	implementation "org.apache.logging.log4j:log4j-core:2.14.0"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.14.0"

	implementation "io.vertx:vertx-web:3.9.2"
	implementation "io.vertx:vertx-pg-client:3.9.2"

	implementation 'org.jooq:jooq:3.13.4'
	implementation 'org.jooq:jooq-meta:3.13.4'

	implementation 'org.postgresql:postgresql:42.2.14'

	implementation "org.flywaydb:flyway-core:7.3.0"

	jooqGenerator 'org.postgresql:postgresql:42.2.14'

	testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

application {
	mainClass = "com.hedera.hashgraph.seven_twenty_one.contract.App"
	mainClassName = "com.hedera.hashgraph.seven_twenty_one.contract.App"
}

jar {
	enabled = false
	manifest {
		attributes "Main-Class": "com.hedera.hashgraph.seven_twenty_one.contract.App"
	}
}

tasks.shadowJar.configure {
	classifier = null
}

tasks.named("test") {
	useJUnitPlatform()
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/seven_twenty_one'
	user = 'postgres'
	password = 'password'
	locations = [
		'filesystem:src/main/resources/migrations'
	]
}

task jooqGenerate {
	doLast() {
		def writer = new StringWriter()

		new MarkupBuilder(writer)
				.configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.13.0.xsd') {
					jdbc {
						driver('org.postgresql.Driver')
						url('jdbc:postgresql://localhost:5432/seven_twenty_one')
						user('postgres')
						password('password')
					}
					generator {
						name('io.github.jklingsporn.vertx.jooq.generate.classic.ClassicReactiveVertxGenerator')
						database {
							name('org.jooq.meta.postgres.PostgresDatabase')
							inputSchema('public')
						}
						generate([:]) {
							deprecated(false)
							records(false)
							interfaces(false)
							fluentSetters(false)
							pojos(false)
							daos(false)
						}
						target() {
							packageName('com.hedera.hashgraph.seven_twenty_one.contract.db')
							directory("$projectDir/src/main/java")
						}
						strategy {
							name('io.github.jklingsporn.vertx.jooq.generate.VertxGeneratorStrategy')
						}
					}
				}

		GenerationTool.generate(
				JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.meta.jaxb.Configuration.class)
				)
	}
}
